# 练习三：分析bootloader进入保护模式的过程

## 分析bootloader是如何完成从实模式进入保护模式的：
首先阅读分析lab1/boot/bootasm.S源码：

```

	#include <asm.h>//bootasm.S代码
	
	# Start the CPU: switch to 32-bit protected mode, jump into C.
	# The BIOS loads this code from the first sector of the hard disk into
	# memory at physical address 0x7c00 and starts executing in real mode//从内存物理地址0x7c00处开始以实模式运行
	# with %cs=0 %ip=7c00.
	
	.set PROT_MODE_CSEG,        0x8                     # kernel code segment selector
	.set PROT_MODE_DSEG,        0x10                    # kernel data segment selector
	.set CR0_PE_ON,             0x1                     # protected mode enable flag
	
	# start address should be 0:7c00, in real mode, the beginning address of the running bootloader//从0x7c00处开始执行bootloader
	.globl start//调用start函数进行初始化
	start:
	.code16                                             # Assemble for 16-bit mode
	    cli                                             # Disable interrupts//屏蔽中断
	    cld                                             # String operations increment//复位向量标志位
	
	    # Set up the important data segment registers (DS, ES, SS).//初始化寄存器为0
	    xorw %ax, %ax                                   # Segment number zero
	    movw %ax, %ds                                   # -> Data Segment
	    movw %ax, %es                                   # -> Extra Segment
	    movw %ax, %ss                                   # -> Stack Segment
	
	    # Enable A20://开启A20地址线
	    #  For backwards compatibility with the earliest PCs, physical
	    #  address line 20 is tied low, so that addresses higher than
	    #  1MB wrap around to zero by default. This code undoes this.
	seta20.1://等待8042键盘控制器不忙
	    inb $0x64, %al                                  # Wait for not busy(8042 input buffer empty).//从0x64端口中读入一个字节到al中 
	    testb $0x2, %al//测试al的第2位
	    jnz seta20.1//al的第2位为0，则跳出循环
	
	    movb $0xd1, %al                                 # 0xd1 -> port 0x64//将0xd1写入al中，开启A20    
	    outb %al, $0x64                                 # 0xd1 means: write data to 8042's P2 port//将0xd1写入到0x64端口中  
	
	seta20.2://等待8042键盘控制器不忙
	    inb $0x64, %al                                  # Wait for not busy(8042 input buffer empty). //从0x64端口中读入一个字节到al中
	    testb $0x2, %al//测试al的第2位
	    jnz seta20.2//al的第2位为0，则跳出循环
	
	    movb $0xdf, %al                                 # 0xdf -> port 0x60//将0xdf入al中,开启 A20
	    outb %al, $0x60                                 # 0xdf = 11011111, means set P2's A20 bit(the 1 bit) to 1//将0xdf入到0x64端口中，打开A20     
	
	    # Switch from real to protected mode, using a bootstrap GDT//从实模式切换到保护模式
	    # and segment translation that makes virtual addresses
	    # identical to physical addresses, so that the
	    # effective memory map does not change during the switch.
	    lgdt gdtdesc//加载全局描述符表 (gdt)
	    movl %cr0, %eax //加载cro到eax
	    orl $CR0_PE_ON, %eax//将eax的第0位置为1
	    movl %eax, %cr0//将cr0的第0位置为1
	
	    # Jump to next instruction, but in 32-bit code segment.
	    # Switches processor into 32-bit mode.
	    ljmp $PROT_MODE_CSEG, $protcseg
	
	.code32                                             # Assemble for 32-bit mode//切换到32位模式
	protcseg:
	    # Set up the protected-mode data segment registers//初始化保护模式下的数据寄存器
	    movw $PROT_MODE_DSEG, %ax                       # Our data segment selector
	    movw %ax, %ds                                   # -> DS: Data Segment
	    movw %ax, %es                                   # -> ES: Extra Segment
	    movw %ax, %fs                                   # -> FS
	    movw %ax, %gs                                   # -> GS
	    movw %ax, %ss                                   # -> SS: Stack Segment
	
	    # Set up the stack pointer and call into C. The stack region is from 0--start(0x7c00)
	    movl $0x0, %ebp //设置帧指针
	    movl $start, %esp //设置栈指针
	    call bootmain//调用bootmain
	
	    # If bootmain returns (it shouldn't), loop.
	spin:
	    jmp spin
	
	# Bootstrap GDT
	.p2align 2                                          # force 4 byte alignment
	gdt:
	    SEG_NULLASM                                     # null seg
	    SEG_ASM(STA_X|STA_R, 0x0, 0xffffffff)           # code seg for bootloader and kernel
	    SEG_ASM(STA_W, 0x0, 0xffffffff)                 # data seg for bootloader and kernel
	
	gdtdesc:
	    .word 0x17                                      # sizeof(gdt) - 1
	    .long gdt                                       # address gdt
```

通过阅读代码我们可以知道从实模式切换进入保护模式的过程如下：

```
	1、清理环境：包括将flag置0和将段寄存器置0
	2、激活A20地址线
	3、加载全局描述符表 (gdt)
	4、打开cr0 ( 开启保护模式)
```

将cr0寄存器PE位置1便开启了保护模式。

## 为何开启A20，以及如何开启A20

当A20地址线控制禁止时，程序就像运行在8086上，1MB以上的地址是不可访问的，当寻址到超过1MB的内存时，会发生“回卷”。为了使能所有地址位的寻址能力，需要打开A20地址线控制。通过向键盘控制器8042发送一个命令来完成开启A20。键盘控制器8042将会将它的的某个输出引脚的输出置高电平，作为 A20 地址线控制的输入。一旦设置成功之后，内存将不会再被绕回(memory wrapping)，这样我们就可以使用全部32根地址线，寻址80386级别机器的所有 4G 内存了。

开启A20的具体步骤：
```
	1、等待8042 Input buffer为空；
	2、发送Write 8042 Output Port （P2）命令到8042 Input buffer；
	3、等待8042 Input buffer为空；
	4、将8042 Output Port（P2）得到字节的第2位置1，然后写入8042 Input buffer；
```

## 如何初始化GDT表

为了使分段存储管理机制正常运行，需要建立好段描述符和段描述符表，全局描述符表是一个保存多个段描述符的“数组”，其起始地址保存在全局描述符表寄存器GDTR中。GDTR长48位，其中高32位为基地址，低16位为段界限。这里只需要载入已经静态存储在引导区的GDT表和其描述符到GDTR寄存器`lgdt gdtdesc//加载全局描述符表 (gdt)`。

## 如何使能和进入保护模式

CR0的位0（PE）是启用保护（Protection Enable）标志。当设置该位时即开启了保护模式；当复位时即进入实地址模式。这个标志仅开启段级保护，而并没有启用分页机制。若要启用分页机制，那么PE和PG标志都要置位。所以这里需要将cr0的PE位置1：
```
 	movl %cr0, %eax //加载cro到eax
	orl $CR0_PE_ON, %eax//将eax的第0位置为1
	movl %eax, %cr0//将cr0的第0位置为1
```

通过长跳转进入保护模式：
```
 ljmp $PROT_MODE_CSEG, $protcseg
```

最后调用bootmain函数
```
call bootmain//调用bootmain
```
